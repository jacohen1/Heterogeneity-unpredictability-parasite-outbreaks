;ask turtles with [breed != gregarines] [show par-list]
;to see the lists of each turtle in the command center

;make turtles have status for infection status, infection load, infection time
turtles-own [
  ;create value for fully established parasite, and list to track
  inf
  inf-list
  ;create value for parasite that may establish, but cannot be detected, and list to track
  par
  par-list
  ;create value for whether the larva has parasites present
  prev
  ;create value for turtle age
  age
]

;make breeds for colonies
breed [
  colony-Bs colony-B
]
breed [
  colony-As colony-A
]

;make global variable for parasites in the flour
globals [
  ;parasites in the flour
  p-env
  ;track age of pupation
  pup-age-list
  ;track number of particles each agent is exposed to
  particle-list
  ]

;code to create a command for random-normal-in-bounds which allows me to limit values for a normal distribution to a known range
to-report random-normal-in-bounds [mid dev mmin mmax]
  let result random-normal mid dev
  if result < mmin or result > mmax
    [ report random-normal-in-bounds mid dev mmin mmax ]
  report result
end

;code to create a command for binomial distribution
to-report random-binomial [n p]
  report length filter [i -> i < p] n-values n [random-float 1]
end

;code to create sigmoid curve which gives chance of dying (pupating) (aiming to be at 100% chance by ~24 days - Leslie & Park 1949)
to-report chances-death
  report ( 1 / ( 1 + ( exp( -1 * ( age - day-of-death ) ) ) ) )
end

;get mean intensity of population
to-report intensity-mean
  ifelse any? turtles with [ (sum inf-list) > 0 ]
  [
    ;calculate intensity
    ;first get a sum of the total number of parasites in infected agents
    let inf-sums sum [sum inf-list] of turtles with [ (sum inf-list) > 0 ]
    ;then divide this number by the number of infected agents to get mean intensity
    let inten inf-sums / count turtles with [ (sum inf-list) > 0 ]
    report inten
  ]
  [
    let inten 0
    report inten
  ]
end

;get list of intensity per agent per timestep
to-report intensity-list
  ifelse any? turtles with [ (sum inf-list) > 0 ]
  [
    ;calculate intensity
    ;first get a sum of the total number of parasites in infected agents
    let inten-list [sum inf-list] of turtles with [ (sum inf-list) > 0 ]
    report inten-list
  ]
  [
    let inten-list (list 0)
    report inten-list
  ]
end

;initial setup code
to setup
  clear-all
  setup-patches
  setup-turtles
  set p-env initial-parasites
  reset-ticks
end

to setup-patches
  ask patches [ set pcolor black ]
end

to setup-turtles
  create-colony-Bs carrying-capacity * (1 - prop-colony-A)
  create-colony-As carrying-capacity * prop-colony-A
  ask turtles [
    setxy random-xcor random-ycor
    set shape "bug"
    set inf 0
    set inf-list ( list inf )
    set prev 0
    set par 0
    set par-list ( list par )
    set age 0
    set pup-age-list []
    set particle-list []
    if (breed = colony-Bs) [
      set color orange
    ]
    if (breed = colony-As) [
      set color red
    ]
  ]
end

;go instructions
to go
  eat-flour-par
  par-to-inf
  inf-tracker
  prev-calc
  par-mort
  increment-age
  if ticks = (day-length * 1500) [
    stop
  ]
  if round p-env <= 0 [
    stop
  ]
  birth-death
  int-label
  tick
end

;instructions for eating flour with a par step
to eat-flour-par
  ask turtles [
    ;get the number of particles each larva will be exposed to, a random poisson distributed number based on the number of parasites and the exposure rate
    let particles random-poisson (p-env * parasite-exposure)
    ;p-env loses the number of particles that were consumed if there are enough, otherwise turtle does not encounter parasites in that turn
    ifelse p-env > particles
    [
      set p-env ( p-env - particles )
      set par ( par + particles )
      set particle-list lput particles particle-list
    ]
    [
      set par ( par + 0 )
      set particle-list lput particles particle-list
    ]
  ]
end

to int-label
  ask turtles [
    ifelse intensity?
    [ set label inf ]
    [ set label "" ]
  ]
end

;instructions for turning par into inf
to par-to-inf
  ask turtles [
    ;get total sum of par-list
    let par-list-sum (sum par-list)
    ;update par-list with change in most recent par value for that turtle
    set par-list lput ( par - par-list-sum) par-list
    ;if the length of each turtle's par-list is longer than one day (assumed time to see established parasite in gut)
    if length par-list > day-length [
      ;split the two processes by colony id
      if breed = colony-Bs [
        ;create value for binomial distribution of parasites establishing, n = uptake, p = susceptibility
        let parasites random-binomial first par-list susc-value-colony-B
        set inf (inf + parasites)
        ;the parasite is lost from the beetle's par-list
        set par-list but-first par-list
      ]
      if breed = colony-As [
        ;create value for binomial distribution of parasites establishing, n = uptake, p = susceptibility
        let parasites random-binomial first par-list susc-value-colony-A
        set inf (inf + parasites)
        ;the parasite is lost from the beetle's par-list
        set par-list but-first par-list
      ]

    ]
  ]
end

to inf-tracker
  ask turtles [
    ;get total sum of inf-list
    let inf-list-sum (sum inf-list)
    ;update inf-list with most recent inf value for that turtle
    set inf-list lput ( inf - inf-list-sum) inf-list
    ;if the length of each turtle's inf-list is longer than the two day incubation period for the parasite after establishment
    if length inf-list > day-length * 2 [
      if breed = colony-Bs [
        ;add shed parasites to p-env, shed parasites are calculated using a poisson distribution to determine the multiplier
        set p-env ( p-env + ( ( random-poisson mean-shed ) * first inf-list ) )
        ;drop the first inf value from its list
        set inf-list but-first inf-list
      ]
      if breed = colony-As [
        ;add shed parasites to p-env, shed parasites are calculated using a poisson distribution to determine the multiplier
        set p-env ( p-env + ( ( random-poisson mean-shed ) * first inf-list ) )
        ;drop the first inf value from its list
        set inf-list but-first inf-list
      ]
    ]
  ]
end

;gives a prevalence value - if there is no parasite then 0, if there is then 1
to prev-calc
  ask turtles [
    ifelse (sum inf-list) > 0 [
      set prev 1 ] [
      set prev 0 ]
  ]
end

to par-mort
  ifelse p-env >= 0
  [ ;let lost p-env - (p-env * random-normal-in-bounds mort-mean mort-sd 0 1 )
    let lost p-env - ( p-env * par-mort-rate )
    ifelse lost >= 0
    [ set p-env lost ]
    [ set p-env 0 ]
  ]
  [ set p-env 0 ]
end

;tracks age of turtles
to increment-age
  ask turtles [
    set age ( 1 + age )
    ;ifelse age?
    ;[ set label age ]
    ;[ set label "" ]
  ]
end

to birth-death
  ask turtles [
    ;if randomly chosen number is < chances, die
    if random-float 1 < chances-death [
      if breed = colony-Bs [
        hatch-colony-Bs 1 [
          setxy random-xcor random-ycor
          set shape "bug"
          set inf 0
          set inf-list ( list inf )
          set prev 0
          set par 0
          set par-list ( list par )
          set age 0
          set color orange
        ]
        set pup-age-list lput age pup-age-list
        die
      ]
      if breed = colony-As [
        hatch-colony-As 1 [
          setxy random-xcor random-ycor
          set shape "bug"
          set inf 0
          set inf-list ( list inf )
          set prev 0
          set par 0
          set par-list ( list par )
          set age 0
          set color red
        ]
        set pup-age-list lput age pup-age-list
        die
      ]
    ]
  ]
  ask turtles [
    ;if randomly chosen number is < chances, die
    if inf >= 1000 [
      if breed = colony-Bs [
        hatch-colony-Bs 1 [
          setxy random-xcor random-ycor
          set shape "bug"
          set inf 0
          set inf-list ( list inf )
          set prev 0
          set par 0
          set par-list ( list par )
          set age 0
          set color orange
        ]
        die
      ]
      if breed = colony-As [
        hatch-colony-As 1 [
          setxy random-xcor random-ycor
          set shape "bug"
          set inf 0
          set inf-list ( list inf )
          set prev 0
          set par 0
          set par-list ( list par )
          set age 0
          set color red
        ]
        die
      ]
    ]
  ]
end
@#$#@#$#@
GRAPHICS-WINDOW
386
10
891
516
-1
-1
29.24
1
10
1
1
1
0
1
1
1
-8
8
-8
8
0
0
1
ticks
30.0

SLIDER
25
109
197
142
prop-colony-A
prop-colony-A
0
1
1.0
0.25
1
NIL
HORIZONTAL

BUTTON
135
20
198
53
NIL
setup
NIL
1
T
OBSERVER
NIL
NIL
NIL
NIL
1

PLOT
1115
10
1579
485
Infectious pressure
time
NIL
0.0
10.0
0.0
10.0
true
false
"" ""
PENS
"default" 1.0 0 -16777216 true "" "plot p-env"

BUTTON
202
20
265
53
NIL
go
T
1
T
OBSERVER
NIL
NIL
NIL
NIL
0

PLOT
1592
11
2081
483
Mean intensity
NIL
NIL
0.0
10.0
0.0
10.0
true
false
"" ""
PENS
"default" 1.0 0 -2674135 true "" "plot intensity-mean"
"pen-1" 1.0 0 -7500403 true "" "plot mean(intensity-list)"

SWITCH
146
68
255
101
intensity?
intensity?
0
1
-1000

SLIDER
25
151
197
184
day-length
day-length
0
100
1.0
1
1
NIL
HORIZONTAL

PLOT
903
10
1103
160
Prevalence
NIL
NIL
0.0
10.0
0.0
1.0
true
false
"" ""
PENS
"default" 1.0 0 -16777216 true "" "plot mean [prev] of turtles"

SLIDER
204
149
376
182
initial-parasites
initial-parasites
0
100000
4000.0
1
1
NIL
HORIZONTAL

SLIDER
205
232
377
265
mean-shed
mean-shed
0
100
30.0
1
1
NIL
HORIZONTAL

SLIDER
25
275
197
308
par-mort-rate
par-mort-rate
0
1
0.1
0.01
1
NIL
HORIZONTAL

SLIDER
203
190
375
223
parasite-exposure
parasite-exposure
0
1
1.0E-4
0.00001
1
NIL
HORIZONTAL

SLIDER
25
193
195
226
susc-value-colony-B
susc-value-colony-B
0
1
0.42
0.01
1
NIL
HORIZONTAL

PLOT
903
173
1103
323
Mean age
NIL
NIL
0.0
100.0
0.0
10.0
true
false
"" ""
PENS
"default" 1.0 0 -16777216 true "" "plot mean [ age ] of turtles"

SLIDER
25
235
195
268
susc-value-colony-A
susc-value-colony-A
0
1
0.26
0.01
1
NIL
HORIZONTAL

SLIDER
205
275
377
308
day-of-death
day-of-death
15
25
23.0
1
1
NIL
HORIZONTAL

MONITOR
134
327
287
372
Median age at pupation
median(pup-age-list)
17
1
11

PLOT
903
330
1103
480
Mean particle exposure
NIL
NIL
0.0
10.0
0.0
3.0
true
false
"" ""
PENS
"default" 1.0 0 -16777216 true "" "plot mean(particle-list)"

SLIDER
204
108
376
141
carrying-capacity
carrying-capacity
0
200
120.0
1
1
NIL
HORIZONTAL

@#$#@#$#@
## WHAT IS IT?

(a general understanding of what the model is trying to show or explain)

## HOW IT WORKS

(what rules the agents use to create the overall behavior of the model)

## HOW TO USE IT

(how to use the model, including a description of each of the items in the Interface tab)

## THINGS TO NOTICE

(suggested things for the user to notice while running the model)

## THINGS TO TRY

(suggested things for the user to try to do (move sliders, switches, etc.) with the model)

## EXTENDING THE MODEL

(suggested things to add or change in the Code tab to make the model more complicated, detailed, accurate, etc.)

## NETLOGO FEATURES

(interesting or unusual features of NetLogo that the model uses, particularly in the Code tab; or where workarounds were needed for missing features)

## RELATED MODELS

(models in the NetLogo Models Library and elsewhere which are of related interest)

## CREDITS AND REFERENCES

(a reference to the model's URL on the web if it has one, as well as any other necessary credits, citations, and links)
@#$#@#$#@
default
true
0
Polygon -7500403 true true 150 5 40 250 150 205 260 250

airplane
true
0
Polygon -7500403 true true 150 0 135 15 120 60 120 105 15 165 15 195 120 180 135 240 105 270 120 285 150 270 180 285 210 270 165 240 180 180 285 195 285 165 180 105 180 60 165 15

arrow
true
0
Polygon -7500403 true true 150 0 0 150 105 150 105 293 195 293 195 150 300 150

box
false
0
Polygon -7500403 true true 150 285 285 225 285 75 150 135
Polygon -7500403 true true 150 135 15 75 150 15 285 75
Polygon -7500403 true true 15 75 15 225 150 285 150 135
Line -16777216 false 150 285 150 135
Line -16777216 false 150 135 15 75
Line -16777216 false 150 135 285 75

bug
true
0
Circle -7500403 true true 96 182 108
Circle -7500403 true true 110 127 80
Circle -7500403 true true 110 75 80
Line -7500403 true 150 100 80 30
Line -7500403 true 150 100 220 30

butterfly
true
0
Polygon -7500403 true true 150 165 209 199 225 225 225 255 195 270 165 255 150 240
Polygon -7500403 true true 150 165 89 198 75 225 75 255 105 270 135 255 150 240
Polygon -7500403 true true 139 148 100 105 55 90 25 90 10 105 10 135 25 180 40 195 85 194 139 163
Polygon -7500403 true true 162 150 200 105 245 90 275 90 290 105 290 135 275 180 260 195 215 195 162 165
Polygon -16777216 true false 150 255 135 225 120 150 135 120 150 105 165 120 180 150 165 225
Circle -16777216 true false 135 90 30
Line -16777216 false 150 105 195 60
Line -16777216 false 150 105 105 60

car
false
0
Polygon -7500403 true true 300 180 279 164 261 144 240 135 226 132 213 106 203 84 185 63 159 50 135 50 75 60 0 150 0 165 0 225 300 225 300 180
Circle -16777216 true false 180 180 90
Circle -16777216 true false 30 180 90
Polygon -16777216 true false 162 80 132 78 134 135 209 135 194 105 189 96 180 89
Circle -7500403 true true 47 195 58
Circle -7500403 true true 195 195 58

circle
false
0
Circle -7500403 true true 0 0 300

circle 2
false
0
Circle -7500403 true true 0 0 300
Circle -16777216 true false 30 30 240

cow
false
0
Polygon -7500403 true true 200 193 197 249 179 249 177 196 166 187 140 189 93 191 78 179 72 211 49 209 48 181 37 149 25 120 25 89 45 72 103 84 179 75 198 76 252 64 272 81 293 103 285 121 255 121 242 118 224 167
Polygon -7500403 true true 73 210 86 251 62 249 48 208
Polygon -7500403 true true 25 114 16 195 9 204 23 213 25 200 39 123

cylinder
false
0
Circle -7500403 true true 0 0 300

dot
false
0
Circle -7500403 true true 90 90 120

face happy
false
0
Circle -7500403 true true 8 8 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Polygon -16777216 true false 150 255 90 239 62 213 47 191 67 179 90 203 109 218 150 225 192 218 210 203 227 181 251 194 236 217 212 240

face neutral
false
0
Circle -7500403 true true 8 7 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Rectangle -16777216 true false 60 195 240 225

face sad
false
0
Circle -7500403 true true 8 8 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Polygon -16777216 true false 150 168 90 184 62 210 47 232 67 244 90 220 109 205 150 198 192 205 210 220 227 242 251 229 236 206 212 183

fish
false
0
Polygon -1 true false 44 131 21 87 15 86 0 120 15 150 0 180 13 214 20 212 45 166
Polygon -1 true false 135 195 119 235 95 218 76 210 46 204 60 165
Polygon -1 true false 75 45 83 77 71 103 86 114 166 78 135 60
Polygon -7500403 true true 30 136 151 77 226 81 280 119 292 146 292 160 287 170 270 195 195 210 151 212 30 166
Circle -16777216 true false 215 106 30

flag
false
0
Rectangle -7500403 true true 60 15 75 300
Polygon -7500403 true true 90 150 270 90 90 30
Line -7500403 true 75 135 90 135
Line -7500403 true 75 45 90 45

flower
false
0
Polygon -10899396 true false 135 120 165 165 180 210 180 240 150 300 165 300 195 240 195 195 165 135
Circle -7500403 true true 85 132 38
Circle -7500403 true true 130 147 38
Circle -7500403 true true 192 85 38
Circle -7500403 true true 85 40 38
Circle -7500403 true true 177 40 38
Circle -7500403 true true 177 132 38
Circle -7500403 true true 70 85 38
Circle -7500403 true true 130 25 38
Circle -7500403 true true 96 51 108
Circle -16777216 true false 113 68 74
Polygon -10899396 true false 189 233 219 188 249 173 279 188 234 218
Polygon -10899396 true false 180 255 150 210 105 210 75 240 135 240

house
false
0
Rectangle -7500403 true true 45 120 255 285
Rectangle -16777216 true false 120 210 180 285
Polygon -7500403 true true 15 120 150 15 285 120
Line -16777216 false 30 120 270 120

leaf
false
0
Polygon -7500403 true true 150 210 135 195 120 210 60 210 30 195 60 180 60 165 15 135 30 120 15 105 40 104 45 90 60 90 90 105 105 120 120 120 105 60 120 60 135 30 150 15 165 30 180 60 195 60 180 120 195 120 210 105 240 90 255 90 263 104 285 105 270 120 285 135 240 165 240 180 270 195 240 210 180 210 165 195
Polygon -7500403 true true 135 195 135 240 120 255 105 255 105 285 135 285 165 240 165 195

line
true
0
Line -7500403 true 150 0 150 300

line half
true
0
Line -7500403 true 150 0 150 150

pentagon
false
0
Polygon -7500403 true true 150 15 15 120 60 285 240 285 285 120

person
false
0
Circle -7500403 true true 110 5 80
Polygon -7500403 true true 105 90 120 195 90 285 105 300 135 300 150 225 165 300 195 300 210 285 180 195 195 90
Rectangle -7500403 true true 127 79 172 94
Polygon -7500403 true true 195 90 240 150 225 180 165 105
Polygon -7500403 true true 105 90 60 150 75 180 135 105

plant
false
0
Rectangle -7500403 true true 135 90 165 300
Polygon -7500403 true true 135 255 90 210 45 195 75 255 135 285
Polygon -7500403 true true 165 255 210 210 255 195 225 255 165 285
Polygon -7500403 true true 135 180 90 135 45 120 75 180 135 210
Polygon -7500403 true true 165 180 165 210 225 180 255 120 210 135
Polygon -7500403 true true 135 105 90 60 45 45 75 105 135 135
Polygon -7500403 true true 165 105 165 135 225 105 255 45 210 60
Polygon -7500403 true true 135 90 120 45 150 15 180 45 165 90

sheep
false
15
Circle -1 true true 203 65 88
Circle -1 true true 70 65 162
Circle -1 true true 150 105 120
Polygon -7500403 true false 218 120 240 165 255 165 278 120
Circle -7500403 true false 214 72 67
Rectangle -1 true true 164 223 179 298
Polygon -1 true true 45 285 30 285 30 240 15 195 45 210
Circle -1 true true 3 83 150
Rectangle -1 true true 65 221 80 296
Polygon -1 true true 195 285 210 285 210 240 240 210 195 210
Polygon -7500403 true false 276 85 285 105 302 99 294 83
Polygon -7500403 true false 219 85 210 105 193 99 201 83

square
false
0
Rectangle -7500403 true true 30 30 270 270

square 2
false
0
Rectangle -7500403 true true 30 30 270 270
Rectangle -16777216 true false 60 60 240 240

star
false
0
Polygon -7500403 true true 151 1 185 108 298 108 207 175 242 282 151 216 59 282 94 175 3 108 116 108

target
false
0
Circle -7500403 true true 0 0 300
Circle -16777216 true false 30 30 240
Circle -7500403 true true 60 60 180
Circle -16777216 true false 90 90 120
Circle -7500403 true true 120 120 60

tree
false
0
Circle -7500403 true true 118 3 94
Rectangle -6459832 true false 120 195 180 300
Circle -7500403 true true 65 21 108
Circle -7500403 true true 116 41 127
Circle -7500403 true true 45 90 120
Circle -7500403 true true 104 74 152

triangle
false
0
Polygon -7500403 true true 150 30 15 255 285 255

triangle 2
false
0
Polygon -7500403 true true 150 30 15 255 285 255
Polygon -16777216 true false 151 99 225 223 75 224

truck
false
0
Rectangle -7500403 true true 4 45 195 187
Polygon -7500403 true true 296 193 296 150 259 134 244 104 208 104 207 194
Rectangle -1 true false 195 60 195 105
Polygon -16777216 true false 238 112 252 141 219 141 218 112
Circle -16777216 true false 234 174 42
Rectangle -7500403 true true 181 185 214 194
Circle -16777216 true false 144 174 42
Circle -16777216 true false 24 174 42
Circle -7500403 false true 24 174 42
Circle -7500403 false true 144 174 42
Circle -7500403 false true 234 174 42

turtle
true
0
Polygon -10899396 true false 215 204 240 233 246 254 228 266 215 252 193 210
Polygon -10899396 true false 195 90 225 75 245 75 260 89 269 108 261 124 240 105 225 105 210 105
Polygon -10899396 true false 105 90 75 75 55 75 40 89 31 108 39 124 60 105 75 105 90 105
Polygon -10899396 true false 132 85 134 64 107 51 108 17 150 2 192 18 192 52 169 65 172 87
Polygon -10899396 true false 85 204 60 233 54 254 72 266 85 252 107 210
Polygon -7500403 true true 119 75 179 75 209 101 224 135 220 225 175 261 128 261 81 224 74 135 88 99

wheel
false
0
Circle -7500403 true true 3 3 294
Circle -16777216 true false 30 30 240
Line -7500403 true 150 285 150 15
Line -7500403 true 15 150 285 150
Circle -7500403 true true 120 120 60
Line -7500403 true 216 40 79 269
Line -7500403 true 40 84 269 221
Line -7500403 true 40 216 269 79
Line -7500403 true 84 40 221 269

wolf
false
0
Polygon -16777216 true false 253 133 245 131 245 133
Polygon -7500403 true true 2 194 13 197 30 191 38 193 38 205 20 226 20 257 27 265 38 266 40 260 31 253 31 230 60 206 68 198 75 209 66 228 65 243 82 261 84 268 100 267 103 261 77 239 79 231 100 207 98 196 119 201 143 202 160 195 166 210 172 213 173 238 167 251 160 248 154 265 169 264 178 247 186 240 198 260 200 271 217 271 219 262 207 258 195 230 192 198 210 184 227 164 242 144 259 145 284 151 277 141 293 140 299 134 297 127 273 119 270 105
Polygon -7500403 true true -1 195 14 180 36 166 40 153 53 140 82 131 134 133 159 126 188 115 227 108 236 102 238 98 268 86 269 92 281 87 269 103 269 113

x
false
0
Polygon -7500403 true true 270 75 225 30 30 225 75 270
Polygon -7500403 true true 30 75 75 30 270 225 225 270
@#$#@#$#@
NetLogo 6.4.0
@#$#@#$#@
@#$#@#$#@
@#$#@#$#@
<experiments>
  <experiment name="RD_initial_run" repetitions="10" runMetricsEveryStep="true">
    <setup>setup</setup>
    <go>go</go>
    <metric>mean [prev] of turtles</metric>
    <metric>intensity</metric>
    <metric>p-env</metric>
    <metric>mean [age] of turtles</metric>
    <enumeratedValueSet variable="parasite-exposure">
      <value value="1.0E-4"/>
    </enumeratedValueSet>
    <steppedValueSet variable="mean-shed-snave" first="10" step="10" last="30"/>
    <enumeratedValueSet variable="day-length">
      <value value="1"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="susc-value-dorris">
      <value value="0.26"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="susc-value-snave">
      <value value="0.42"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="intensity?">
      <value value="true"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="day-of-death">
      <value value="23"/>
    </enumeratedValueSet>
    <steppedValueSet variable="mean-shed-dorris" first="10" step="10" last="30"/>
    <steppedValueSet variable="Num-snave" first="0" step="10" last="40"/>
    <enumeratedValueSet variable="initial-parasites">
      <value value="4000"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="par-mort-rate">
      <value value="0.1"/>
    </enumeratedValueSet>
  </experiment>
  <experiment name="RD_initial_run_2" repetitions="200" runMetricsEveryStep="true">
    <setup>setup</setup>
    <go>go</go>
    <metric>mean [prev] of turtles</metric>
    <metric>intensity</metric>
    <metric>p-env</metric>
    <metric>mean [age] of turtles</metric>
    <enumeratedValueSet variable="parasite-exposure">
      <value value="1.0E-4"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="mean-shed-snave">
      <value value="30"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="day-length">
      <value value="1"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="susc-value-dorris">
      <value value="0.26"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="susc-value-snave">
      <value value="0.42"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="intensity?">
      <value value="true"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="day-of-death">
      <value value="23"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="mean-shed-dorris">
      <value value="18"/>
    </enumeratedValueSet>
    <steppedValueSet variable="Num-dorris" first="0" step="10" last="40"/>
    <enumeratedValueSet variable="initial-parasites">
      <value value="4000"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="par-mort-rate">
      <value value="0.1"/>
    </enumeratedValueSet>
  </experiment>
  <experiment name="Trib_3.0_run" repetitions="250" runMetricsEveryStep="true">
    <setup>setup</setup>
    <go>go</go>
    <metric>mean [prev] of turtles</metric>
    <metric>intensity-mean</metric>
    <metric>intensity-list</metric>
    <metric>p-env</metric>
    <metric>mean [age] of turtles</metric>
    <enumeratedValueSet variable="parasite-exposure">
      <value value="1.0E-4"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="day-length">
      <value value="1"/>
    </enumeratedValueSet>
    <steppedValueSet variable="proportion-dorris" first="0" step="0.25" last="1"/>
    <enumeratedValueSet variable="susc-value-dorris">
      <value value="0.26"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="susc-value-snave">
      <value value="0.42"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="carrying-capacity">
      <value value="120"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="day-of-death">
      <value value="23"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="mean-shed">
      <value value="30"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="initial-parasites">
      <value value="4000"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="par-mort-rate">
      <value value="0.1"/>
    </enumeratedValueSet>
  </experiment>
</experiments>
@#$#@#$#@
@#$#@#$#@
default
0.0
-0.2 0 0.0 1.0
0.0 1 1.0 0.0
0.2 0 0.0 1.0
link direction
true
0
Line -7500403 true 150 150 90 180
Line -7500403 true 150 150 210 180
@#$#@#$#@
0
@#$#@#$#@
